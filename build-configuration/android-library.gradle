/**
 * Base build configuration for all android libraries (modules applying the android library plugin).
 *
 * Don't add any configuration here that is not common to *every* android library.
 */
apply plugin: "com.android.library"
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"
apply plugin: "'com.google.dagger.hilt.android"

apply from: rootProject.file('build-configuration/detekt.gradle')
apply from: rootProject.file('build-configuration/ktlint.gradle')

assemble.dependsOn('lint')
check.dependsOn('checkstyle')
check.dependsOn('ktlint')

configurations {
    javadocDeps
}

android {
    compileSdkVersion 33
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33
        consumerProguardFiles 'proguard-rules.txt'
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // From https://developer.android.com/training/testing/junit-runner:
        // > To remove all shared state from your device's CPU and memory after each test,
        // > use the clearPackageData flag.
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation(libs.hilt.android)
    kapt(libs.hilt.compiler)
    testImplementation(libs.hilt.android.test)
    testImplementation(libs.junit)
    testImplementation(libs.mockk.android)
    testImplementation(libs.kotlinx.coroutines.test)
    testImplementation(libs.turbine)
}
kapt {
    correctErrorTypes true
}
hilt {
    enableAggregatingTask = true
}
